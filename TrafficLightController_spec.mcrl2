
% Data Types Definitions: 
sort 	
	LightColorState = struct Green | Orange | Red;
	WeatherCondition = struct Dry | Snowing | Raining;

sort
	TrafficRequestStates = List(Bool);
map
	requestGreenOnLightP: TrafficRequestStates # Nat # Nat # Bool -> TrafficRequestStates;

	% Mappings
	requestGreenOnLight: TrafficRequestStates # Nat -> TrafficRequestStates;
	clearRequestOnLight: TrafficRequestStates # Nat -> TrafficRequestStates;
var
	rs: TrafficRequestStates;
	i: Nat;
	t: Nat;
	s: Bool;
eqn
	requestGreenOnLightP([], i, t, s) = [];
	i == t -> requestGreenOnLightP(rs, i, t, s) = s |> requestGreenOnLightP(tail(rs), i + 1, t, s);
	i != t -> requestGreenOnLightP(rs, i, t, s) = head(rs) |> requestGreenOnLightP(tail(rs), i + 1, t, s);

	% Mappings
	requestGreenOnLight(rs, t) = requestGreenOnLightP(rs, 0, t, true);
	clearRequestOnLight(rs, t) = requestGreenOnLightP(rs, 0, t, false);

% Constants
map
 	TRAFFIC_LIGHT_COUNT: Nat;
eqn
	TRAFFIC_LIGHT_COUNT = 3;

% Actions Definitions: External 
act 
	ShowLight: Nat # LightColorState;
	LightTimeout: Nat # LightColorState;
	UsersDetected: Nat # Bool;
	EVApproaching: Nat;
	EVPassed: Nat;
	Weather: WeatherCondition;

% Actions Definitions: Internal
act 
	EVPoll: Nat;
	WeatherPoll;
	CycleDone: Nat;
	BadWeather: Bool;
	RequestGreen: Nat;
	EVDetected: Nat # Bool;

% Process: 
proc 
	LightComponent(t: Nat, color: LightColorState) =
		(color == Red) -> 
			sum any_traffic_light: Nat . (any_traffic_light < TRAFFIC_LIGHT_COUNT) ->
				RequestGreen(any_traffic_light) .
				ShowLight(any_traffic_light, Green) .
				LightComponent(any_traffic_light, Green)
		+
		(color == Green) ->
			LightTimeout(t, Green) .
			ShowLight(t, Orange) .
			LightComponent(t, Orange)
		+
		(color == Orange) ->
			LightTimeout(t, Orange) .
			ShowLight(t, Red) .
			CycleDone(t) .
			LightComponent(t, Red)
	;
	EVComponent(lanes: TrafficRequestStates) =
		sum t: Nat. (t < TRAFFIC_LIGHT_COUNT) ->
			EVApproaching(t) .
			EVComponent(requestGreenOnLight(lanes, t))
		+
		sum t: Nat. (t < TRAFFIC_LIGHT_COUNT) ->
			EVPassed(t) .
			EVComponent(clearRequestOnLight(lanes, t))
		+
		sum t: Nat. (t < TRAFFIC_LIGHT_COUNT) ->
			EVPoll(t) .
			EVDetected(t, lanes . t) .
			EVComponent(lanes)
	;
	WeatherComponent(current: WeatherCondition) =
		sum new: WeatherCondition .
			Weather(new) .
			WeatherComponent(new)
		+
			WeatherPoll .
			(
				(current == Dry) -> BadWeather(false) +
				(current != Dry) -> BadWeather(true)
			) .
			WeatherComponent(current)
	;


init WeatherComponent(Dry);
