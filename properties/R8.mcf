
% ------- REQUIREMNETS BASED ON DOC: v1.6 ------ % 

% ======== Natural Language Specification ========
% After for any traffic light t element of T, 
% UserDetected(t, true)$ and $ShowLight(t, red) occurred, 
% no t' in intersecting(t), may have more than two 
% occurrences of $ShowLight(t', green) before 
% traffic light t has an occurrence of 
% ShowLight(t, green)$ or $EmergencyVApproaching(t', true).
% ================================================

forall t: Nat.
  forall t': Nat. 
    val(t != t' && 
        t  < TRAFFIC_LIGHT_COUNT && 
        t' < TRAFFIC_LIGHT_COUNT) => 

  nu X(n : Nat = 0, UserPresent : Bool = false, LightRed : Bool = true, EVPresent : Bool = false).
    [ShowLight(t, Red)]       X(n, UserPresent, true, EVPresent) &&
    [ShowLight(t, Green)]     X(0, UserPresent, false, EVPresent) &&
    [UsersDetected(t, true)]  X(n, true, LightRed, EVPresent) &&
    [UsersDetected(t, false)] X(n, false, LightRed, EVPresent) &&
    [EVDetected(t', false)]    X(n, UserPresent, LightRed, false) &&
    [EVDetected(t', true)]    X(0, UserPresent, LightRed, true) &&
    (val(LightRed && UserPresent && !EVPresent) => [ShowLight(t', Green)] 
                              X(n + 1, UserPresent, LightRed, EVPresent)) &&
    (val(!(LightRed && UserPresent && !EVPresent)) => [ShowLight(t', Green)] 
                              X(n, UserPresent, LightRed, EVPresent)) &&
    val(n < 3) &&
    [!(ShowLight(t, Red) || ShowLight(t, Green) || UsersDetected(t, true) ||
       UsersDetected(t, false) || ShowLight(t', Green) || EVDetected(t', false)|| EVDetected(t', true))]
      X(n, UserPresent, LightRed, EVPresent)