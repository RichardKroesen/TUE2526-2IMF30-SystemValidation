
% ------- REQUIREMNETS BASED ON DOC: v1.6 ------ % 

% ======== Natural Language Specification ========
% After for any traffic light t element of T, 
% UserDetected(t, true)$ and $ShowLight(t, red) occurred, 
% no t' in intersecting(t), may have more than two 
% occurrences of $ShowLight(t', green) before 
% traffic light t has an occurrence of 
% ShowLight(t, green)$ or $EmergencyVApproaching(t', true).
% ================================================

forall t: Nat.
  forall t': Nat. 
    val(t != t' && 
        t  < TRAFFIC_LIGHT_COUNT && 
        t' < TRAFFIC_LIGHT_COUNT) => 

  nu X(n : Nat = 0, UserPresent : Bool = false, LightRed : Bool = true, EVPresent : Bool = false, BadWeather : Bool = false).
	[Weather(Raining) || Weather(Snowing)]X(n, UserPresent, LightRed, EVPresent, true) &&
    [Weather(Dry)]X(n, UserPresent, LightRed, EVPresent, false) &&
    [ShowLight(t, Red)]       X(n, UserPresent, true, EVPresent, BadWeather) &&
    [ShowLight(t, Green)]     X(0, UserPresent, false, EVPresent, BadWeather) &&
    [UsersDetected(t, true)]  X(n, true, LightRed, EVPresent, BadWeather) &&
    [UsersDetected(t, false)] X(0, false, LightRed, EVPresent, BadWeather) &&
    [EVDetected(t', false)]    X(n, UserPresent, LightRed, false, BadWeather) &&
    [EVDetected(t', true)]    X(0, UserPresent, LightRed, true, BadWeather) &&

	(val(LightRed && UserPresent && !EVPresent && (t' in PED_SET => !BadWeather)) => [ShowLight(t', Green)] 
                              X(n + 1, UserPresent, LightRed, EVPresent, BadWeather)) &&

    (val(!(LightRed && UserPresent && !EVPresent && (t' in PED_SET => !BadWeather))) => [ShowLight(t', Green)] 
                              X(n, UserPresent, LightRed, EVPresent, BadWeather)) &&

    val(n < 3) &&
    [!(ShowLight(t, Red) || ShowLight(t, Green) || UsersDetected(t, true) ||
       UsersDetected(t, false) || ShowLight(t', Green) || EVDetected(t', false)|| EVDetected(t', true) || Weather(Raining) || Weather(Snowing) || Weather(Dry))]
      X(n, UserPresent, LightRed, EVPresent, BadWeather)