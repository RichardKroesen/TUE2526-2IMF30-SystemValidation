
% ------- REQUIREMNETS BASED ON DOC: v1.6 ------ % 

% ======== Natural Language Specification ========
% When observation variable badW eather = true 
% then for all p element of P when
% UserDetected(p, true) then ShowLight(p, green) 
% happens before for all t element T \ {P}
% ShowLight(t, green) if for all 
% EmergencyVApproaching(t, false). If there 
% exists U serDetected(t, true) then at least one 
% ShowLight(t, green) happens before 
% UserDetected(p, true) happens again.
% ================================================

forall t: Nat, p: Nat.
  val(t < (TRAFFIC_LIGHT_COUNT) && p in PED_SET && !(t in PED_SET)) =>
  nu X(Priority: Bool = false, BadWeather: Bool = false,
       EVPresent: Bool = false, UserPresent: Bool = false). (
    [Weather(Raining) || Weather(Snowing)]X(true, true, EVPresent, UserPresent) &&
    [Weather(Dry)]X(Priority, false, EVPresent, UserPresent) &&
    [UsersDetected(p, true)]    X(Priority, BadWeather, EVPresent, true) &&
    [UsersDetected(p, false)]   X(Priority, BadWeather, EVPresent, false) &&
    [EVDetected(t, true)]       X(Priority, BadWeather, true, UserPresent) &&
    [EVDetected(t, false)]      X(Priority, BadWeather, false, UserPresent) &&
    (val(BadWeather) => (
      (val(Priority) => (
        [ShowLight(p, Green)]   X(false, BadWeather, EVPresent, UserPresent) &&
        (val(!EVPresent && UserPresent) => [ShowLight(t, Green)]false) &&
        (val(!(!EVPresent && UserPresent)) => [ShowLight(t, Green)]
                                X(Priority, BadWeather, EVPresent, UserPresent))
      )) &&
      (val(!Priority) => (
        [ShowLight(t, Green)]   X(true, BadWeather, EVPresent, UserPresent) &&
        [ShowLight(p, Green)]   X(Priority, BadWeather, EVPresent, UserPresent)
      ))
    )) &&
    (val(!BadWeather) => [ShowLight(t, Green) || ShowLight(p, Green)]
      X(Priority, BadWeather, EVPresent, UserPresent)) &&
    [!(Weather(Raining) || Weather(Snowing) || Weather(Dry) ||
       UsersDetected(p, false) || UsersDetected(p, true) ||
       EVDetected(t, true) || EVDetected(t, false) ||
       ShowLight(t, Green) || ShowLight(p, Green))]
      X(Priority, BadWeather, EVPresent, UserPresent)
  )
